" Automatically install vim-plug {{{
if empty(glob('~/.vim/autoload/plug.vim'))
    silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
                \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif
" }}}

" Plugins {{{
call plug#begin()
Plug 'junegunn/vim-plug'
command -nargs=? PI PlugInstall <args>
command PC PlugClean
command PU PlugUpgrade | PlugUpdate

" CoC {{{
Plug 'neoclide/coc.nvim', {'branch': 'fix/vim-onTextChange', 'do': 'npm ci'}
Plug 'xi-lef/coc-snippets', {'do': 'yarn install'}
cnoreabbrev CI CocInstall
cnoreabbrev CU CocUninstall
cnoreabbrev CC CocCommand
let g:coc_global_extensions = [
            \ 'coc-calc',
            \ 'coc-clangd',
            \ 'coc-cmake',
            \ 'coc-css',
            \ 'coc-git',
            \ 'coc-html',
            \ 'coc-java',
            \ 'coc-json',
            \ 'coc-markdownlint',
            \ 'coc-pyright',
            \ 'coc-sh',
            \ 'coc-tsserver',
            \ 'coc-vimlsp',
            \ 'coc-vimtex'
            \]
let g:coc_status_warning_sign = '⚠️ '
let g:coc_status_error_sign = '❗'

" Insert-mappings for auto-completion {{{
inoremap <silent><expr> <C-Space> coc#refresh()
inoremap <silent><expr> <Tab> coc#pum#visible() ? coc#pum#next(1) : '<Tab>'
inoremap <silent><expr> <S-Tab> coc#pum#visible() ? coc#pum#prev(1) : ''
inoremap <silent><expr> <CR> coc#pum#visible() ? coc#_select_confirm() : '<CR>'
inoremap <silent><nowait><expr> <C-B> coc#float#has_scroll() ? '<C-R>=coc#float#scroll(0)<CR>' : '<Left>'
inoremap <silent><nowait><expr> <C-F> coc#float#has_scroll() ? '<C-R>=coc#float#scroll(1)<CR>' : '<Right>'
nnoremap <silent><nowait><expr> <C-B> coc#float#has_scroll() ? coc#float#scroll(0) : '<C-B>'
nnoremap <silent><nowait><expr> <C-F> coc#float#has_scroll() ? coc#float#scroll(1) : '<C-F>'
" }}}

" Movement-mappings {{{
nmap [e <Plug>(coc-diagnostic-prev)
nmap ]e <Plug>(coc-diagnostic-next)
nmap [d <Plug>(coc-git-prevchunk)
nmap ]d <Plug>(coc-git-nextchunk)
nmap gd <Plug>(coc-definition)
nmap <C-W>gd <C-W>v <Plug>(coc-definition)
" }}}

" Complex actions {{{
nmap <Leader>r <Plug>(coc-rename)
nmap <Leader>q <Plug>(coc-fix-current)
nnoremap <silent> <Leader>h :CocCommand clangd.switchSourceHeader<CR>
nnoremap <silent> <C-W><Leader>h :CocCommand clangd.switchSourceHeader vsplit<CR>
nnoremap <silent> <Leader>i :call CocActionAsync('doHover')<CR>
nnoremap <silent> <Leader>t :CocCommand document.toggleInlayHint<CR>
nnoremap <silent> <Leader>a :CocCommand git.chunkStage<CR>
nnoremap <silent> <Leader>d :CocCommand git.chunkInfo<CR>
nnoremap <silent> <Leader>u :CocCommand git.chunkUndo<CR>
nnoremap <silent> <Leader>l :CocCommand git.showCommit<CR>
nnoremap <silent> <Leader>g :CocCommand git.toggleGutters<CR>
command OI :call CocAction('runCommand', 'editor.action.organizeImport')

augroup coc
    " Highlight the symbol and its references when holding the cursor
    autocmd CursorHold * silent! call CocActionAsync('highlight')
    autocmd CursorHold * silent! CocCommand git.refresh
augroup END
" }}}
" }}}

" VimTeX {{{
Plug 'lervag/vimtex'
let g:tex_flavor = 'latex'
"let g:vimtex_compiler_method = 'generic' | let g:vimtex_compiler_generic = {'cmd': 'make'}
let g:vimtex_view_method = 'zathura'
let g:vimtex_quickfix_autoclose_after_keystrokes = 1
" Map ctrl+space to forward search
nmap <silent> <C-@> <Plug>(vimtex-view)
nmap <F8> <Plug>(vimtex-compile)
if empty(v:servername) && exists('*remote_startserver')
    silent! call remote_startserver('VIM') " necessary for backward search
endif
" }}}

" Filetype-specific plugins {{{
Plug 'iamcco/markdown-preview.nvim', {'do': 'cd app && yarn install'}
let g:mkdp_auto_close = 0
nmap <C-P> <Plug>MarkdownPreviewToggle

Plug 'tikhomirov/vim-glsl'
Plug 'sheerun/vim-polyglot'
let g:polyglot_disabled = ['autoindent', 'latex'] " conflict with vimtex

Plug 'jackguo380/vim-lsp-cxx-highlight'
Plug 'mogelbrod/vim-jsonpath'

Plug 'vim-autoformat/vim-autoformat'
Plug 'xi-lef/vim-clang-format'
let g:clang_format#code_style = 'google'
let g:clang_format#style_options = {
            \ 'AccessModifierOffset': -4,
            \ 'BreakBeforeBinaryOperators': 'All',
            \ 'DerivePointerAlignment': 'false',
            \ 'IndentWidth': 4,
            \ 'PointerAlignment': 'Right'
            \ }
nmap <silent> <Leader>f <Plug>(operator-clang-format)
vnoremap <silent> <Leader>f :ClangFormat<CR>

Plug 'honza/vim-snippets'
" }}}

" 'Graphical' plugins {{{
Plug 'majutsushi/tagbar'
nnoremap <silent> <F6> :TagbarToggle<CR>
Plug 'preservim/nerdtree'
let g:NERDTreeBookmarksFile = $HOME . '/.vim/NERDTreeBookmarks'
let g:NERDTreeShowBookmarks = 1
let g:NERDTreeShowHidden = 1
let g:NERDTreeMinimalUI = 1
nnoremap <silent> <C-N> :e .<CR>
nnoremap <silent> <C-W><C-N> :rightbelow vsplit .<CR>
" }}}

" Miscellaneous plugins {{{
Plug 'google/vim-searchindex'
let g:searchindex_star_case = 0
Plug 'bronson/vim-visual-star-search'

Plug 'ntpeters/vim-better-whitespace'
let g:show_spaces_that_precede_tabs = 1
Plug 'tpope/vim-sleuth'
Plug 'Yggdroot/indentLine'
let g:indentLine_enabled = 0 " Use :IndentLines{Enable,Disable,Toggle}

Plug 'machakann/vim-highlightedyank'
let g:highlightedyank_highlight_duration = 500
Plug 'gcavallanti/vim-noscrollbar'
Plug 'matze/vim-move'
let g:move_key_modifier = 'C'
Plug 'farmergreg/vim-lastplace'

Plug 'kana/vim-operator-user' " For vim-clang-format
" }}}

" Statusline {{{
Plug 'itchyny/lightline.vim'
let g:lightline = {
    \ 'colorscheme': 'one',
    \ 'active': {
    \   'left': [['mode', 'paste'],
    \            ['cocstatus', 'readonly', 'filename', 'modified', 'scrollbar']],
    \   'right': [['lineinfo'],
    \             ['percent'],
    \             ['fileformat', 'fileencoding', 'filetype']]
    \ },
    \ 'component_function': {
    \     'cocstatus': 'coc#status',
    \     'scrollbar': 'noscrollbar#statusline'
    \ }
\ }
autocmd User CocStatusChange,CocDiagnosticChange call lightline#update()
" }}}

Plug 'NLKNguyen/papercolor-theme'

call plug#end()
" }}}

" Functions {{{
" Inserts an SP-grading comment, with the arrow pointing to the position of the
" cursor
function SPComment(print_arrow = 1)
    if &filetype ==# 'c' || expand('%') ==# 'machfile'
        let start = '/*'
        let middle = ''
        let end = '*/'
    elseif &filetype ==# 'make'
        let start = '##'
        let middle = '# '
        let end = '#/'
    else
        echoerr 'Unrecognized filetype'
        return
    endif

    " the correct amount of spaces (depending on the cursor) follow by '^'
    let arrow = !a:print_arrow ? '' : repeat(' ', virtcol('.') - 4) . '^'
    " helper-'function' to clear a line
    let clear = "\<Esc>0\"_Di"

    execute 'normal! o' . clear . start . 'K' . arrow . "\<CR>"
                \ . clear . end . "\<Esc>O" . clear . middle
    startinsert!
endfunction
" }}}

" Mappings {{{
" Miscellaneous {{{
nnoremap <silent> <F5> :source $MYVIMRC<CR>:redraw!<CR>
nnoremap <silent> <F7> :set spell!<CR>
nnoremap <silent> <Space> :nohlsearch<CR>
nnoremap <silent> <Leader>c :call SPComment()<CR>
nnoremap <silent> <Leader>C :call SPComment(0)<CR>
nnoremap <silent> <Leader>S :call SPComment(0) <Bar> :normal! aSchoen. :)<C-O>0<CR>
nnoremap Y y$
noremap ' `
nnoremap p ]p
nnoremap P [p
inoremap <C-C> <Esc>

" Open terminal in various ways
nmap <silent> <C-W>t :vertical terminal<CR>
tmap <silent> <C-W>t <C-W>:vertical terminal<CR>
nmap <silent> <C-W><C-T> :terminal ++curwin<CR>

" Set colors
nnoremap <silent> <F2> :let &background = (&background == 'dark' ? 'light' : 'dark')<CR>
tnoremap <silent> <F2> <C-W>:let &background = (&background == 'dark' ? 'light' : 'dark')<CR>

" 'Competitive Programming'
nnoremap <F9> :write <Bar> !g++ -O3 -o %:r %<CR>
" }}}

" Movements {{{
" Use map instead of noremap for vim-smoothie
map <C-S> <C-U>
map <C-Q> <C-Y>

inoremap <C-J> <C-O>j
inoremap <C-K> <C-O>k
noremap! <A-f> <C-O>w
noremap! <A-b> <C-O>b

noremap j gj
ounmap j
noremap k gk
ounmap k
" }}}

" Tabs {{{
" Open new, empty tab
nnoremap <silent> <C-T> :tabedit<CR>
tnoremap <silent> <C-T> <C-W>:tabedit<CR>

" Map alt+<i> to switch to tab <i>
nnoremap <A-1> 1gt
nnoremap <A-2> 2gt
nnoremap <A-3> 3gt
nnoremap <A-4> 4gt
nnoremap <A-5> 5gt
nnoremap <A-6> 6gt
nnoremap <A-7> 7gt
nnoremap <A-8> 8gt
nnoremap <A-9> 9gt
nnoremap <A-0> 10gt
nnoremap <A--> 11gt
nnoremap <A-=> 12gt
" Move tabs with ctrl-shift-page{up,down}
nnoremap <silent> <C-PageUp> :silent! tabm -<CR>
nnoremap <silent> <C-PageDown> :silent! tabm +<CR>

" Same as above, but for terminal windows
tnoremap <silent> <A-1> <C-W>:1tabnext<CR>
tnoremap <silent> <A-2> <C-W>:2tabnext<CR>
tnoremap <silent> <A-3> <C-W>:3tabnext<CR>
tnoremap <silent> <A-4> <C-W>:4tabnext<CR>
tnoremap <silent> <A-5> <C-W>:5tabnext<CR>
tnoremap <silent> <A-6> <C-W>:6tabnext<CR>
tnoremap <silent> <A-7> <C-W>:7tabnext<CR>
tnoremap <silent> <A-8> <C-W>:8tabnext<CR>
tnoremap <silent> <A-9> <C-W>:9tabnext<CR>
tnoremap <silent> <A-0> <C-W>:10tabnext<CR>
tnoremap <silent> <A--> <C-W>:11tabnext<CR>
tnoremap <silent> <A-=> <C-W>:12tabnext<CR>
tnoremap <silent> <C-PageUp> <C-W>:silent! tabm -<CR>
tnoremap <silent> <C-PageDown> <C-W>:silent! tabm +<CR>
" }}}
" }}}

" Commands {{{
" Sessions
command -bang -bar -nargs=1 CS execute 'mksession<bang> ' . $HOME . '/.vim/sessions/' . '<args>.vim'
command -bar SS execute 'mksession! ' . v:this_session
command -bar XS SS <Bar> wall <Bar> qall!
command -bar DS execute 'silent !rm ' . v:this_session <Bar> redraw!
command QQ qall!

" Shortcuts for opening vsplits
command -nargs=1 -complete=file LS leftabove vsplit <args>
command -nargs=1 -complete=file RS rightbelow vsplit <args>

" Facilitate writing as sudo
command W write !sudo tee > /dev/null %
" }}}

" Theme {{{
"set t_Co=256
set t_ut=
let &t_SI = "\e[5 q"
let &t_EI = "\e[2 q"
set background=dark
"set termguicolors
" Darker background: value 233 is 'blackgravel' from 'sjl/badwolf' (232 is even darker)
let g:PaperColor_Theme_Options = {
            \   'theme': {
            \       'default.dark': {
            \           'override': {
            \               'color00' : ['#1c1b1a', '233']
            \           }
            \       }
            \   }
            \ }
colorscheme PaperColor
" }}}

" Filetype-specific {{{
augroup filetype_e2
    autocmd!
    autocmd BufRead,BufNewFile *.e2 setlocal filetype=e2
    autocmd BufRead,BufNewFile *.ir setlocal filetype=e2ir
augroup END

augroup filetype_json
    autocmd!
    autocmd FileType json,jsonc setlocal foldmethod=syntax
    autocmd FileType json,jsonc nnoremap <silent> <buffer> <localleader>j :call jsonpath#echo()<CR>
augroup END

augroup filetype_python
    autocmd!
    autocmd FileType python IndentLinesEnable
augroup END

augroup filetype_tex
    autocmd!
    autocmd FileType tex setlocal textwidth=0
augroup END

augroup filetype_vim
    autocmd!
    autocmd FileType vim setlocal foldmethod=marker
augroup END

augroup filetype_vsfs
    autocmd!
    autocmd BufNewFile,BufRead *.vs,*.fs set filetype=glsl
augroup END
" }}}

" Options {{{
syntax enable               " enable syntax highlighting
set scrolloff=5             " lines above/below cursor when scrolling
set number                  " show line numbers
set title                   " window title is set reasonably
set noshowmode              " do not show mode in last line
set ignorecase              " ignore case when searching
set fileignorecase          " ignore case for :b, :e etc.
set smartcase               " override ignorecase when searching for upper case pattern
set incsearch               " highlight search results while typing
set hlsearch                " highlight search results after hitting enter
"set shortmess-=S            " show number of search results
set wildmenu                " show command-line matches
set showcmd                 " show size of selection in visual mode
set noerrorbells            " nothing annoying on errors
set novisualbell            " ^
set wrap                    " wrap lines longer than the window
set linebreak               " wrap lines at word boundaries
set showbreak=>\            " wrapped lines start with '> '
set breakindent             " wrapped lines are visually indented
set laststatus=2            " always show statusline
set tabstop=4               " 1 tab == 4 spaces
set shiftwidth=0            " use tabstop-value
set textwidth=80            " max 80 characters per line
set expandtab               " use spaces instead of tabs
set smarttab                " smart shit, idk
set list                    " show whitespace (e.g. tabs)
set autoindent              " copy indent from adjacent line
set smartindent             " smart, language-based indenting
set signcolumn=number       " show signs (from ycm/coc) in place of line numbers
set splitright              " new windows open on the right

set encoding=utf8
set backspace=indent,eol,start
set autochdir               " automatically cd to directory of file in buffer
set clipboard=unnamedplus   " share vim-clipboard with system-clipboard
set cryptmethod=blowfish2   " encryption (:X)
set spelllang=de,en         " for use with spellchecking
set viminfofile=$HOME/.vim/viminfo
set viminfo='30,<10000,s1000
"           |   |      + 1000 kb maximum size for registers
"           |   + 10000 lines maximum for registers
"           + remember marks for last 30 files
set sessionoptions-=buffers " do not save hidden/unloaded buffers in sessions
set sessionoptions-=options " do not save options/mappings in sessions
set sessionoptions+=localoptions " do save local options (e.g. for spelling)
set termwinscroll=100000    " more terminal scrollback
set updatetime=100          " delay (e.g. for tagbar auto-refresh)
set ttimeoutlen=100         " less delay for <S-O>

" Highlight the current line (number) in insert-mode. Only set 'cursorlineopt' while in insert-mode
" as to not influence NERDTree.
autocmd InsertEnter * set cursorline cursorlineopt=number
autocmd InsertLeave * set nocursorline cursorlineopt=both
" }}}

" Miscellaneous {{{
" Do not display line numbers for terminals (when in normal mode)
if exists('##TerminalWinOpen')
    autocmd TerminalWinOpen * setlocal nonumber
endif

" Use powershell on windows
if has('win32') || has('win64')
    set shell=powershell
endif
" }}}
